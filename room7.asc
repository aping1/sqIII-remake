// room script file

int GrabberX;
int GrabberY;

bool RogerTop;

function room_RepExec()
{
  // if (player.x>166) hBackRound.Enabled = true;
}

function room_Load()
{
  CameFromRoom = 7;
  
  hComputers.Enabled=false;
  
  if ((InGrabber==true) && (player.y==100)) {                  // ENTERS FROM RIGHT, IN GRABBER, BACKGROUND
    SetWalkBehindBase(2, 130);
    cControllerRobot.Baseline = 140;
    SetWalkBehindBase(5, 200);
    SetWalkBehindBase(3, 200);
    hBackRound.Enabled = true;
    RemoveWalkableArea(1);
  }
  
  else if ((InGrabber==true) && (player.y==166)) {            // ENTERS FROM RIGHT, IN GRABBER, FOREGROUND
    SetWalkBehindBase(2, 0);
    SetWalkBehindBase(5, 0);
    SetWalkBehindBase(3, 0);
    cControllerRobot.Baseline = 99;
    hBackRound.Enabled = false;
    RemoveWalkableArea(1);
  }
  
  else if (InGrabber==false) {                                // ENTERS FROM RIGHT, ON FOOT
    SetWalkBehindBase(2, 0);
    SetWalkBehindBase(5, 0);
    SetWalkBehindBase(3, 0);
    cControllerRobot.Baseline = 99;
    hBackRound.Enabled = false;
  }
}


function room_AfterFadeIn()
{
  if ((InGrabber==true) && (player.y==100)) {                  // ENTER FROM RIGHT, IN GRABBER, BACKGROUND
    player.Walk(230, 100, eBlock);
    hComputers.Enabled=true;
  }
  else if ((InGrabber==true) && (player.y==166)) {            // ENTER FROM RIGHT, IN GRABBER, FOREGROUND
    player.Walk(290, 166, eBlock, eAnywhere);
    hComputers.Enabled=true;
  }
  else if (InGrabber==false) {                                // ENTER FROM RIGHT, ON FOOT
    player.Walk(290, 101, eBlock, eAnywhere);
    hComputers.Enabled=true;
  }
}

function room_LeaveRight()
{
  if (InGrabber==false) {                                       // LEAVING TO RIGHT, ON FOOT
    player.Walk(350, 100, eBlock);
    player.ChangeRoom(8, -50, 131);
  }
  else if ((InGrabber==true) && (player.y==166)) {              // LEAVING TO RIGHT, IN GRABBER, FOREGROUND
    player.Walk(350, 166, eBlock);
    player.ChangeRoom(8, 0, 195);
  }
}

function hComputers_WalkOn()
{
  if ((InGrabber==true) && (player.y==100)) {                    // LEAVING TO RIGHT, IN GRABBER, BACKGROUND
    player.Walk(330, 100, eBlock);
    player.ChangeRoom(8, 75, 135);
  }
}

function hPipes_Mode9()
{
  Display("There's a foul metallic smell coming off these pipes.");
}

function hPipes_Mode8()
{
  Display("You give a whiff in the direction of the pipes, but whatever smell they might possess is overpowered by the olfactory offensiveness of the rest of this place.");
}

function hPipes_Interact()
{
  Display("You can't reach them.");
}

function hPipes_Look()
{
  Display("Who knows what these pipes are for. Waste? Propulsion? Air ventilation? You'll never guess and you'll never find out.");
}

function hGeneral_Interact()
{
  Display("Your instincts, accident-prone as they may be, tell you that you don't want to make too much noise here.");
}

function oGrabber_Look()
{
  Display("A grabber hangs beneath a rail. There is a seat for a driver. There is a claw underneath that looks capable of grabbing things.");
}

function oGrabberM_Look()
{
  Display("A grabber hangs beneath a rail. There is a seat for a driver. There is a claw underneath that's currently holding a warp motivator.");
}


function oGrabber_Interact()
{
  if (oGrabber.X==154) {
    player.Walk(154, 150, eBlock);
    Display("You climb back into the grabber.");
  }
  else if (oGrabber.X==237) {
    player.Walk(250, 101, eBlock);
    Display("You climb into the grabber.");    
  }
  
  if (GrabberHasMotivator==true) player.ChangeView(15);
  else player.ChangeView(10);
  
  if (oGrabber.X==154) {                              // Grabber is near the chute
    player.x = 163; player.y = 166;
    player.Baseline = 0;
    RemoveWalkableArea(1); RestoreWalkableArea(2);
    player.TurnBeforeWalking = false;
  }
  
  else if (oGrabber.X==237) {                         // Grabber is near the start
    player.x = 257; player.y = 166;
    SetWalkBehindBase(4, 195);
    RemoveWalkableArea(1); RestoreWalkableArea(2);
    player.TurnBeforeWalking = false;
  }
  
  //Play climbing animation
  player.FaceDirection(eDirectionDown);
  player.LockView(33, eStopMoving);
  player.Baseline = 170;
  player.x = (player.x)+5;
  //player.y = (player.y)+60;
  player.Animate(0, 10, eOnce, eBlock, eForwards);
  player.UnlockView(eStopMoving);
  player.Baseline = 0;
  player.x = (player.x)-5;
  
  player.FaceDirection(eDirectionLeft);
  
  
  oGrabber.Visible = false;
  InGrabber = true;
  gGrabber.Visible = true;
  
  //mouse.DisableMode(eModeWalkto);
}

function oGrabberM_Interact()
{
  if (oGrabberM.X==154) {
    player.Walk(154, 150, eBlock);
    Display("You climb back into the grabber.");
  }
  else if (oGrabberM.X==237) {
    player.Walk(250, 101, eBlock);
    Display("You climb into the grabber.");
  }
  
  if (GrabberHasMotivator==true) player.ChangeView(15);
  else player.ChangeView(10);
  
  if (oGrabberM.X==154) {                              // Grabber is near the chute
    player.x = 163; player.y = 166;
    player.Baseline = 0;
    RemoveWalkableArea(1); RestoreWalkableArea(2);
    player.TurnBeforeWalking = false;
  }
  
  else if (oGrabberM.X==237) {                         // Grabber is near the start
    player.x = 257; player.y = 166;
    SetWalkBehindBase(4, 195);
    RemoveWalkableArea(1); RestoreWalkableArea(2);
    player.TurnBeforeWalking = false;
  }
  
  oGrabberM.Visible = false;
  InGrabber = true;
  gGrabber.Visible = true;
  
  //mouse.DisableMode(eModeWalkto);
}


function hGetOff_Look()
{
  if (InGrabber==true) Display("There seems to be a potential for disembarkation here.");
  else Display("The center garbage chute has a platform attached to it.");
}

function hGetOff_Interact()
{
  if (InGrabber==true) {
    if ((player.x > 130) && (player.x < 180) && (player.y==166)) {
      player.Walk(154, (player.y));
      
      if (GrabberHasMotivator==true) {
        oGrabberM.X = 154; oGrabberM.Y = 166;
        oGrabberM.Visible = true;
        oGrabberM.Baseline = 100;
      }
      else {
        oGrabber.X = 154; oGrabber.Y = 166;
        oGrabber.Visible = true;
        oGrabber.Baseline = 100;
      }
      player.ChangeView(1);
      player.Baseline = 200;
      player.Move(150, 160, eBlock, eAnywhere);
      player.FaceDirection(eDirectionDown);
      gGrabber.Visible = false;
      InGrabber = false;
      RestoreWalkableArea(1);
      player.TurnBeforeWalking = true;
    }
    else Display("You can't quite reach it. You'll have to move the grabber closer to the platform.");
  }
  else Display("You can't jump down there from here.");
}

function hRail_Look()
{
  if (InGrabber==true) Display("You're gliding along the rail you were previously occupying on foot. Click the USE icon on it to disembark.");
  else Display("The rail rounds a corner at the far end of the room. Be careful. It's hard enough to keep your balance when it's straight, never mind when it bends!");
}

function hRail_Interact()
{
  if ((InGrabber==true) && (player.y==166)) {
    Display("You move the grabber back to where you found it and climb back onto the precarious rail.");
    player.Walk(240, (player.y), eBlock, eAnywhere);
    
    if (GrabberHasMotivator==true) {
      oGrabberM.X = 237;
      oGrabberM.Visible = true;
    }
    else {
      oGrabber.X = 237;
      oGrabber.Visible = true;
    }    
    
    player.ChangeView(1);
    player.FaceDirection(eDirectionLeft);    
    
    SetWalkBehindBase(4, 0);
    gGrabber.Visible = false;
    InGrabber = false;
    RemoveWalkableArea(2); RestoreWalkableArea(1);
    player.TurnBeforeWalking = true;
    
    //Play climbing animation backwards
    player.FaceDirection(eDirectionDown);
    player.LockView(33, eStopMoving);
    player.x = (player.x)+5;
    player.y = 161;
    player.Animate(0, 10, eOnce, eBlock, eBackwards);
    player.UnlockView(eStopMoving);
    
    player.x = (player.x)-5;
    player.y = 101;
    
  }
  else if ((InGrabber==true) && (player.y==77)) Display("There's nowhere to get off the grabber here.");
  else Display("You consider crawling along the rail instead of walking but for some reason you don't do it. Why don't you do it? That would be so much easier! Oh, well.");
}

function room_LeaveLeft()
{
  if (InGrabber==true) {
    player.Walk(10, 166, eBlock, eAnywhere);
    player.Walk(5, 156, eBlock, eAnywhere);
    SetWalkBehindBase(2, 130);
    cControllerRobot.Baseline = 140;
    SetWalkBehindBase(5, 200);
    SetWalkBehindBase(3, 200);
    player.Walk(60, 141, eBlock, eAnywhere);
    if (GrabberHasMotivator==true) player.ChangeView(14);
    else player.ChangeView(11);
    Wait(20);
    player.x = 155; player.y = 100;
    player.Walk(200, 100, eBlock);
    RemoveWalkableArea(1); RestoreWalkableArea(2);
    hBackRound.Enabled = true;
    // RogerTop=true;
  }
  else {
    player.Move(0, 210, eBlock, eAnywhere);
    deathHeader.Text = "Nice footwork!";
    deathMessage.Text = "Guess rounding that bend on foot takes a little more acrobatic skill than you'll ever possess.";
    YoureDead();
  }
}

function hGarbageChute_WalkOn()
{
  player.Move((player.x), 250, eBlock, eAnywhere);
  player.ChangeRoom(9, 60, 40);
}

function hBackRound_WalkOn()
{
  player.Walk(150, 100, eBlock, eAnywhere);
  player.Move(60, 141, eBlock, eAnywhere);
  if (GrabberHasMotivator==true) player.ChangeView(15);
  else player.ChangeView(10);
  player.Walk(5, 156, eBlock, eAnywhere);
  cControllerRobot.Baseline = 96;
  SetWalkBehindBase(2, 0);
  SetWalkBehindBase(5, 0);
  SetWalkBehindBase(3, 0);
  player.Walk(10, 166, eBlock, eAnywhere);
  player.Walk(40, 166, eBlock);
  hBackRound.Enabled = false;
}



