// room script file

int GrabberX;
int GrabberY;

function room_AfterFadeIn()
{
  CameFromRoom = 7;
  
  if ((InGrabber==true) && (player.y==97)) {                  // ENTER FROM RIGHT, IN GRABBER, BACKGROUND
    player.Walk(230, 97, eBlock);
  }
  else if ((InGrabber==true) && (player.y==160)) {            // ENTER FROM RIGHT, IN GRABBER, FOREGROUND
    player.Walk(290, 160, eBlock, eAnywhere);
  }
  else if (InGrabber==false) {                                // ENTER FROM RIGHT, ON FOOT
    player.Walk(290, 101, eBlock, eAnywhere);
  }
}

function room_LeaveRight()
{
  if (InGrabber==false) {                                       // LEAVING TO RIGHT, ON FOOT
    player.Walk(350, 100, eBlock);
    player.ChangeRoom(8, -50, 131);
  }
  else if ((InGrabber==true) && (player.y==160)) {              // LEAVING TO RIGHT, IN GRABBER, FOREGROUND
    player.Walk(350, 160, eBlock);
    player.ChangeRoom(8, 0, 190);
  }
  else if ((InGrabber==true) && (player.y==97)) {               // LEAVING TO RIGHT, IN GRABBER, BACKGROUND
    player.Walk(330, 97, eBlock);
    player.ChangeRoom(8, 85, 132);
  }
}

function hPipes_Mode9()
{
  Display("Luckily for everyone, the pipes are out of your reach.");
}

function hPipes_Mode8()
{
  Display("You give a whiff in the direction of the pipes, but whatever smell they might possess is overpowered by the olfactory offensiveness of the rest of this place.");
}

function hPipes_Interact()
{
  Display("You can't reach them.");
}

function hPipes_Look()
{
  Display("Who knows what these pipes are for. Waste? Propulsion? Air ventilation? You'll never guess and you'll never find out.");
}

function hGeneral_Interact()
{
  Display("Your instincts, accident-prone as they may be, tell you that you don't want to make too much noise here.");
}

function oGrabber_Interact()
{
  if (oGrabber.X==154) {
    player.Walk(154, 150, eBlock);
    Display("You climb back into the grabber.");
  }
  else if (oGrabber.X==237) {
    player.Walk(250, 101, eBlock);
    Display("You climb into the grabber.");
  }
  player.ChangeView(10);
  
  if (oGrabber.X==154) {                              // Grabber is near the chute
    player.x = 163; player.y = 160;
    SetWalkBehindBase(4, 240);
    RemoveWalkableArea(1);
  }
  
  else if (oGrabber.X==237) {                         // Grabber is near the start
    player.x = 257; player.y = 160;
    SetWalkBehindBase(4, 240);
    RemoveWalkableArea(1);
  }
  
  oGrabber.Visible = false;
  InGrabber = true;
  gGrabber.Visible = true;
  
  mouse.DisableMode(eModeWalkto);
  Display("Use left/right arrows on your keyboard to move the grabber.");
}

function hGetOff_Look()
{
  if (InGrabber==true) Display("There seems to be a potential for disembarkation here.");
  else Display("The center garbage chute has a platform attached to it.");
}

function hGetOff_Interact()
{
  if (InGrabber==true) {
    if ((player.x > 130) && (player.x < 180) && (player.y==160)) {
      player.Walk(154, (player.y));
      oGrabber.X = 154; oGrabber.Y = 160;
      oGrabber.Visible = true;
      player.ChangeView(1);
      SetWalkBehindBase(4, 0);
      player.Move(150, 160, eBlock, eAnywhere);
      player.FaceDirection(eDirectionDown);
      gGrabber.Visible = false;
      InGrabber = false;
      RestoreWalkableArea(1);
      mouse.EnableMode(eModeWalkto);
    }
    else Display("You can't quite reach it. You'll have to move the grabber closer to the platform.");
  }
  else Display("You can't jump down there from here.");
}

function hRail_Look()
{
  if (InGrabber==true) Display("You're gliding along the rail you were previously occupying on foot. Click the USE icon on it to disembark.");
  else Display("The rail rounds a corner at the far end of the room. Be careful. It's hard enough to keep your balance when it's straight, never mind when it bends!");
}

function hRail_Interact()
{
  if ((InGrabber==true) && (player.y==160)) {
    Display("You move the grabber back to where you found it and climb back onto the precarious rail.");
    player.Walk(240, (player.y), eBlock, eAnywhere);
    oGrabber.X = 237;
    oGrabber.Visible = true;
    player.ChangeView(1);
    SetWalkBehindBase(4, 0);
    player.Move(240, 101, eBlock, eAnywhere);
    gGrabber.Visible = false;
    InGrabber = false;
    RestoreWalkableArea(1);
    mouse.EnableMode(eModeWalkto);
  }
  else if ((InGrabber==true) && (player.y==77)) Display("There's nowhere to get off the grabber here.");
  else Display("You consider crawling along the rail instead of walking but for some reason you don't do it. Why don't you do it? That would be so much easier! Oh, well.");
}

function room_LeaveLeft()
{
  if (InGrabber==true) {
    hBackRound.Enabled = false;
    player.Walk(10, 160, eBlock, eAnywhere);
    player.Walk(5, 150, eBlock, eAnywhere);
    SetWalkBehindBase(2, 200);
    SetWalkBehindBase(3, 200);
    player.Walk(60, 135, eBlock, eAnywhere);
    player.ChangeView(11);
    player.x = 150; player.y = 97;
    player.Walk(200, 97, eBlock);
    hBackRound.Enabled = true;
  }
  else {
    player.Move(0, 210, eBlock, eAnywhere);
    deathHeader.Text = "Nice footwork!";
    deathMessage.Text = "Guess rounding that bend on foot takes a little more acrobatic skill than you'll ever possess.";
    YoureDead();
  }
}

function hGarbageChute_WalkOn()
{
  player.Move((player.x), 250, eBlock, eAnywhere);
  player.ChangeRoom(9, 60, 40);
}

function hBackRound_WalkOn()
{
  player.Walk(150, 97, eBlock, eAnywhere);
  player.Move(60, 135, eBlock, eAnywhere);
  player.ChangeView(10);
  player.Walk(5, 150, eBlock, eAnywhere);
  SetWalkBehindBase(2, 0);
  SetWalkBehindBase(3, 0);
  player.Walk(10, 160, eBlock, eAnywhere);
  player.Walk(40, 160, eBlock);
}


// ROBOT

function oDroid_Interact()
{
  Display("The robot is too far away.");
}

function oDroid_Look()
{
  Display("A lone robot tends to some maintenance controls here. It seems harmless enough.");
}

function oDroid_Mode8()
{
  Display("You can't tell from here but he probably smells of efficiency.");
}

function oDroid_Talk()
{
  player.SpeechView = 5;
  player.FaceCharacter(cControllerRobot);
  player.Say("Um, excuse me?");
  Wait(20);
  Display("The robot does not appear to be voice activated.");
}

function oDroid_Mode9()
{
  player.FaceCharacter(cControllerRobot);
  Display("He probably wouldn't appreciate you distracting him with your bizarre behavior.");
}