// room script file

int OldXSpeed;
int OldYSpeed;

function room_Load()
{
  oConveyer.SetView(7);
  oConveyer.Animate(0, 5, eRepeat, eNoBlock, eBackwards);
  oGrinder.SetView(8);
  oGrinder.Animate(0, 5, eRepeat, eNoBlock, eForwards);
  oEngineGlow.SetView(9);
  oEngineGlow.Animate(0, 5, eRepeat, eNoBlock, eForwards);
  
  if (player.y==190) {                                       // ENTERING IN GRABBER, FOREGROUND
    SetWalkBehindBase(2, 0);
    SetWalkBehindBase(3, 0);
  }
  if (player.y==112) {                                        // ENTERING IN GRABBER, BACKGROUND
    SetWalkBehindBase(2, 200);
    SetWalkBehindBase(3, 200);
  }
  if (player.y==131) {                                       // ENTERING BY FOOT FROM LEFT
    SetWalkBehindBase(2, 0);
  }
}

function room_AfterFadeIn()
{
  if (TravelByBucket==true) {
    player.Move(455, 130, eBlock, eAnywhere);
    player.Move(500, 120, eBlock, eAnywhere);
    player.Move(510, 140, eBlock, eAnywhere);
    player.Move(515, 153, eBlock, eAnywhere);
    TravelByBucket = false;
    OhNoConveyer = true;
      OldXSpeed = player.WalkSpeedX;
      OldYSpeed = player.WalkSpeedY;
    player.SetWalkSpeed(1, 1);
    player.Move(650, player.y, eNoBlock, eAnywhere);
  }
  else if ((CameFromRoom==7) && (player.y==131)) {             // ENTERING BY FOOT FROM LEFT
    player.Walk(100, 131, eBlock, eAnywhere);
  }
  else if ((CameFromRoom==7) && (player.y==190)) {             // ENTERING IN GRABBER, FOREGROUND
    player.Walk(150, 190, eBlock, eAnywhere);
  }
  else if ((CameFromRoom==7) && (player.y==112)) {              // ENTERING IN GRABBER, BACKGROUND
    player.Walk(120, 112, eBlock, eAnywhere);
  }
}

function hGrabOnRail_Interact()
{
  if (OhNoConveyer==true) {
    player.FaceDirection(eDirectionDown);
    Wait(5);
    player.StopMoving();
    OhNoConveyer = false;
    player.Move((player.x), 131, eBlock, eAnywhere);
    player.SetWalkSpeed((OldXSpeed), (OldYSpeed));
    SetWalkBehindBase(2, 0);
    Display("Phew! That was a close one!");
  }
  else Display("Don't climb back down there! Don't you remember where that goes?");
}

function room_LeaveLeft()
{
  if (player.y==131) {                                            // EXITING BY FOOT
    player.Walk(-30, 131, eBlock, eAnywhere);
    player.ChangeRoom(7, 380, 101);
  }
  else if (player.y==190) {                                       // EXITING IN GRABBER, FOREGROUND
    player.Walk(0, 190, eBlock, eAnywhere);
    player.ChangeRoom(7, 320, 160);
  }
}

function hLeftInGrabberBG_WalkOn()
{
  player.Walk(80, 112, eBlock, eAnywhere);                         // EXITING IN GRABBER, BACKGROUND
  player.ChangeRoom(7, 255, 77);
}
