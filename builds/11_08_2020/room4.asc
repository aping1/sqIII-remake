bool IsRogerOnShip;

// room script file

function room_Load()
{
  // GRABBING GRABBY THINGS WITH GRABBER
  
  if (GrabberActive==true) {
    player.Transparency = 100;            // Hide Roger sprite
  }
  else player.Transparency = 0;
}


function room_AfterFadeIn()
{
  if (GrabberActive==true) {
    if (GrabberDumpMotivator==true) {
      oClawM.Visible = true;
      oClawM.X = 283; oClawM.Y = -10;
      oClawM.Move(283, 57, 1, eBlock, eAnywhere);
      Wait(80);
      oClawM.Visible = false;
      oClaw.Visible = true;
      oClaw.X = 283; oClaw.Y = 55;
      Display("Sensing an adequate surface, the claw releases its cargo and begins the ascent to the grabber unit. The object thuds into place within the cavity of the ship.");
      MotivatorDumpedYay = true;
      GrabberDumpMotivator = false;
      GrabberHasMotivator = false;
    }
    else if (GrabberHasMotivator==true) {
      oClawM.Visible = true;
      oClawM.X = ((player.x)-640); oClaw.Y = -10;
      oClawM.Move(((player.x)-640), 175, 1, eBlock, eAnywhere);
      Wait(80);
      Display("Sensing no adequate compartment to install its payload in, the grabber claw returns to the grabber.");
      oClawM.Move(((player.x)-640), 0, 1, eBlock, eAnywhere);
      oClawM.Visible = false;
    }
    else {
      oClaw.Visible = true;
      oClaw.X = ((player.x)-640); oClaw.Y = -10;
      oClaw.Move(((player.x)-640), 175, 1, eBlock, eAnywhere);
      Wait(80);
      Display("Finding nothing here to carry, the claw begins the ascent to the grabber unit.");
      oClaw.Move(((player.x)-640), 0, 1, eBlock, eAnywhere);
      oClaw.Visible = false;
    }
  GrabberActive = false;
  GrabberReturning = true;
  player.ChangeRoom(8);
  }
  
  
}



function hRamp_Interact()
{
  player.Walk(232, 45, eBlock);
  player.ChangeRoom(3, 188, 171);
}

function hHatch_Interact()
{
  if (IsRogerOnShip==true) {
    if ((player.x < 205) && (player.y < 85)) Display("You'll need to get closer.");
    else {
      player.Walk(208, 80, eBlock);
      player.FaceDirection(eDirectionRight);
      player.ChangeRoom(5, 180, 160);
    }
  }
  else Display("You can't do that from down here.");
}


