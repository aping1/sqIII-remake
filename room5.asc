// room script file

function hHatch_Interact()
{
  player.Walk(180, 160, eBlock);
  //SetBackgroundFrame(0);
  Display("You stand on one of the chairs, open the hatch, and somehow manage to pull yourself up out of the ship.");
  player.ChangeRoom(4);
}

function room_AfterFadeIn(){
}

function room_Load(){
  if( Mallard.HasPower() ){ 
    SetBackgroundFrame( 1 );
    oDiagnosticMonitor.Graphic = 423;
  }
  else SetBackgroundFrame( 0 );
  
  if( player.PreviousRoom == -1 ){
    player.AddInventory( iGenerator );
    player.AddInventory( iWire );
  }
}

function hFloorCompartment_Look(){
  if( ! Mallard.HasReactor && ! Mallard.HasWires ){
    Display( "You look into the cavity and notice only two cable ends. Someone has made off with the ship's power supply." );
  } else if( ! Mallard.HasReactor ){
    Display( "You look into the cavity. The wire is firmly attached to cable ends." );
  } else if( ! Mallard.HasWires ){
    Display( "You look into the cavity at the reactor. It's currently non-functional." );
  }
}

function oMallardFloorPanel_Look()
{
  Display( "The access panel is firmly in place." );
}

function oMallardFloorPanel_Interact(){
  Display( "The tile is doing a nice job of covering the hole, just leave it alone." );
}

function hFloorCompartment_UseInv(){
  if( player.ActiveInventory == iGenerator ){
    player.LoseInventory( iGenerator );

    if( Game.DoOnceOnly( "GiveScore_InstallReactorInShip" ) )
      GiveScore( 5 );
      
    if( Mallard.HasWires ){
      Display( "You carefully connect the wire between the ship and the reactor, putting the tile back in place once you've finished." );
    } else{
      Display( "You drop the reactor into the hole. In attempting to reconnect the cables, you find that one is much too short." );
    }
    Mallard.HasReactor = true;
  }
  else if( player.ActiveInventory == iWire ){
    player.LoseInventory( iWire );

    if( Game.DoOnceOnly( "GiveScore_InstallWiresInShip" ) )
      GiveScore( 5 );
    
    if( Mallard.HasReactor ){
      Display( "You carefully connect the wire between the ship and the reactor, putting the tile back in place once you've finished." );
    } else{
      Display( "You install the wire inside the reactor compartment." );
    }
    Mallard.HasWires = true;
  }
  
  if( Mallard.HasPower() ){
    oMallardFloorPanel.Visible = true;
    Wait( 10 );
    SetBackgroundFrame( 1 );
    oDiagnosticMonitor.Graphic = 423;
  }
}
