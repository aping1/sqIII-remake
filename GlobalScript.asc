// Called when the game starts, before the first room is loaded
function game_start() {   
  // Put the code all in a function and then just call the function. 
  // It saves cluttering up places like game_start.
  initialize_control_panel(); 
  // Use the KeyboardMovement module to, per default, replicate the standard
  // keyboard movement of most Sierra games. See KeyboardMovement.txt for more info
  KeyboardMovement.SetMode(eKeyboardMovement_Tapping); 
  
  SetGameUI( "New" );
}

function repeatedly_execute() {  
  // Put here anything you want to happen every game cycle, even when
  // the game is paused. This will not run when the game is blocked
  // inside a command like a blocking Walk()
  if (IsGamePaused() == 1) return;
  // Put here anything you want to happen every game cycle, but not
  // when the game is paused.
  
}

function repeatedly_execute_always() {  
  // Put anything you want to happen every game cycle, even
  // when the game is blocked inside a command like a
  // blocking Walk().
  // You cannot run blocking commands from this function.
  
}

// Called when a key is pressed. keycode holds the key's ASCII code
function on_key_press(eKeyCode keycode) {
  // The following is called before "if game is paused keycode=0", so
  // it'll happen even when the game is paused.
  
  if (keycode == eKeyEscape)
    HandleEscapeKey();
  if (keycode == eKeyReturn) { 
    // ENTER, in this case merely confirms restart
    if (gRestart.Visible) RestartGame();
  }

  if (IsGamePaused() || (IsInterfaceEnabled() == 0))
  {
    // If the game is paused with a modal GUI on the
    // screen, or the player interface is disabled in
    // a cut scene, ignore any keypresses.
    return;
  }

  // FUNCTION KEYS AND SYSTEM SHORTCUTS
  if (keycode == eKeyCtrlQ) ShowQuitGUI();   // Ctrl-Q
  if (keycode == eKeyF5) ShowSaveGameGUI();   // F5
  if (keycode == eKeyF7) ShowRestoreGameGUI();  // F7
  if (keycode == eKeyF9) ShowRestartGUI();
  if (keycode == eKeyF12) SaveScreenShot("scrnshot.bmp");  // F12
  if (keycode == eKeyTab)   ShowInventoryGUI();  // Tab, show inventory

  // GAME COMMAND SHORTCUTS
  if (keycode == 'W') mouse.Mode=eModeWalkto; //Notice this alternate way to indicate keycodes.
  if (keycode == 'L') mouse.Mode=eModeLookat; //Note that all we do here is set modes.
  if (keycode == 'U') mouse.Mode=eModeInteract; //If you want something else to happen, such as GUI buttons highlighting,
  if (keycode == 'T') mouse.Mode=eModeTalkto; //you'll need some more scripting done.
  if (keycode == 'I') mouse.Mode=eModeUseinv; //But this will, as-is, give you some standard keyboard shortcuts your players will very much appreciate.

  // For extra cursor modes, such as pick up, feel free to add as you will.
  // Uncomment the line below if you use the "Pick Up" mode.
  //if (keycode == 'P' || keycode == 'G') mouse.Mode=eModePickup; 
  
  if( keycode == eKeyCtrlU ) SwapGameUI();
}

// called when a mouse button is clicked. button is either LEFT or RIGHT
function on_mouse_click(MouseButton button) {
  if (IsGamePaused() == 1) {
    if( gInventory.Visible == true ){
      if( button == eMouseWheelNorth ){
        invCustomInv.ScrollUp();
      } else if( button == eMouseWheelSouth ){
        invCustomInv.ScrollDown();
      }
    }
    
  }
  else if (button == eMouseLeft) {
    Room.ProcessClick(mouse.x, mouse.y, mouse.Mode );
  }
  else if (button == eMouseRight || button == eMouseWheelSouth){
    // right-click our mouse-wheel down, so cycle cursor
    HandleRightClick();
  }
  else if (button == eMouseMiddle) { 
    // Middle-button-click, default make character walk to clicked area (a little shortcut)
    // Could have been just "player.Walk(mouse.x,mouse.y)", but it's best to
    // leave our options open - what if you have a special script triggered
    // on "walking" mode?
    // Room.ProcessClick(mouse.x, mouse.y, eModeWalkto); 
    mouse.Mode = eModeWalkto;
  }
  else if (button == eMouseWheelNorth) { 
    // Mouse-wheel up, cycle cursors 
    // If mode isn't WALK, set the previous mode (notice usage of numbers instead
    // of eNums, when it suits us)...
    if (mouse.Mode>0) mouse.Mode=mouse.Mode-1; 
    else 
    { 
      // ...but if it is WALK mode...
      if (player.ActiveInventory!=null) 
      {
        //...and the player has a selected inventory item, set mouse mode to UseInv. 
        mouse.Mode=eModeUseinv; 
      }
      else 
      {
        // If they don't, however, just set it to mode TALK (change this line if you add more cursor modes)
        mouse.Mode=eModeTalkto; 
      }
    }
  }
}

function interface_click(int interface, int button) {
  // This function is obsolete, from 2.62 and earlier versions.
}

function btnInvUp_Click(GUIControl *control, MouseButton button) {
  invCustomInv.ScrollUp();
}

function btnInvDown_Click(GUIControl *control, MouseButton button) {
  invCustomInv.ScrollDown();
}

function btnInvOK_Click(GUIControl *control, MouseButton button) {
	// They pressed the OK button, close the GUI
	gInventory.Visible = false;
	mouse.UseDefaultGraphic();
}

function btnInvSelect_Click(GUIControl *control, MouseButton button) {  
	// They pressed SELECT, so switch to the Get cursor
	mouse.Mode = eModeInteract;
	// But, override the appearance to look like the arrow
	mouse.UseModeGraphic(eModePointer);
}

function btnIconInv_Click(GUIControl *control, MouseButton button) {
  ShowInventoryGUI();
}

function btnIconCurInv_Click(GUIControl *control, MouseButton button) {
  
  if (player.ActiveInventory != null)
    mouse.Mode = eModeUseinv;
}

function btnIconSave_Click(GUIControl *control, MouseButton button) 
{
  ShowSaveGameGUI();
}

function btnIconLoad_Click(GUIControl *control, MouseButton button) 
{
  ShowRestoreGameGUI();
}

function btnIconExit_Click(GUIControl *control, MouseButton button) {
  ShowQuitGUI();
}

function btnIconAbout_Click(GUIControl *control, MouseButton button) {
  
  Display("Help?");
}


//START OF CONTROL PANEL FUNCTIONS

function gControl_OnClick(GUI *theGui, MouseButton button)
{

}

function btnAbout_OnClick(GUIControl *control, MouseButton button)
{
Display("Adventure Game Studio run-time engine default game.");
}

function btnQuit_OnClick(GUIControl *control, MouseButton button)
{
  if (UIType=="New") gNewPanel.Visible = false; else gPanel.Visible = false;
  Wait(1);
  ShowQuitGUI();
  if (UIType=="New") gNewPanel.Visible = true; else gPanel.Visible = true;
  gIconbar.Visible = false;
  mouse.UseModeGraphic(eModePointer);
}

function btnResume_OnClick(GUIControl *control, MouseButton button)
{
  if (UIType=="New") gNewPanel.Visible = false; else gPanel.Visible = false;
  mouse.UseDefaultGraphic();
  gIconbar.Visible = true;
}

function sldAudio_OnChange(GUIControl *control)
{
  System.Volume = sldAudio.Value;
}

function sldMusic_OnChange(GUIControl *control)
{
  System.Volume = sldMusic.Value;
}

function sldVoice_OnChange(GUIControl *control)
{
  // Sets voice volume. Note that we don't check for the existence of speech.vox - 
  // we did that in game_start, so if it's not there the slider won't even be available.
  SetSpeechVolume(sldVoice.Value); 
}

function btnVoice_OnClick(GUIControl *control, MouseButton button)
{
  // Note that we don't check for the existence of speech.vox - we did that in game_start,
  // so if it's not there the button won't even be available.
  
  if (UIType=="Classic") {
    //IF speech is currently ON and text is ON, disable speech
    if ((btnVoice.NormalGraphic==306) && (btnText.NormalGraphic==311)) {
      btnVoice.NormalGraphic = 305;
      Speech.VoiceMode = eSpeechTextOnly;
    }
    //IF speech is currently ON and text is OFF, disable speech but enable text
    else if ((btnVoice.NormalGraphic==306) && (btnText.NormalGraphic==310)) {
      btnVoice.NormalGraphic = 305;
      btnText.NormalGraphic = 311;
      Speech.VoiceMode = eSpeechTextOnly;
    }
    //IF speech is currently OFF and text is ON, enable both
    else if ((btnVoice.NormalGraphic==305) && (btnText.NormalGraphic==311)) {
      btnVoice.NormalGraphic = 306;
      Speech.VoiceMode = eSpeechVoiceAndText;
    }
  }  
  else if (UIType=="New") {
    //IF speech is currently ON and text is ON, disable speech
    if ((bNewSpeech.NormalGraphic==284) && (bNewText.NormalGraphic==289)) {
      bNewSpeech.NormalGraphic = 283;
      Speech.VoiceMode = eSpeechTextOnly;
    }
    //IF speech is currently ON and text is OFF, disable speech but enable text
    else if ((bNewSpeech.NormalGraphic==284) && (bNewText.NormalGraphic==288)) {
      bNewSpeech.NormalGraphic = 283;
      bNewText.NormalGraphic = 289;
      Speech.VoiceMode = eSpeechTextOnly;
    }
    //IF speech is currently OFF and text is ON, enable both
    else if ((bNewSpeech.NormalGraphic==283) && (bNewText.NormalGraphic==289)) {
      bNewSpeech.NormalGraphic = 284;
      Speech.VoiceMode = eSpeechVoiceAndText;
    }
  }
}

function btnText_OnClick(GUIControl *control, MouseButton button)
{
  if (UIType=="Classic") {
    //IF speech is currently ON and text is ON, disable text
    if ((btnVoice.NormalGraphic==306) && (btnText.NormalGraphic==311)) {
      btnText.NormalGraphic = 310;
      Speech.VoiceMode = eSpeechVoiceOnly;
    }
    //IF speech is currently OFF and text is ON, disable text but enable speech
    else if ((btnVoice.NormalGraphic==305) && (btnText.NormalGraphic==311)) {
      btnVoice.NormalGraphic = 306;
      btnText.NormalGraphic = 310;
      Speech.VoiceMode = eSpeechVoiceOnly;
    }
    //IF speech is currently ON and text is OFF, enable both
    else if ((btnVoice.NormalGraphic==306) && (btnText.NormalGraphic==310)) {
      btnText.NormalGraphic = 311;
      Speech.VoiceMode = eSpeechVoiceAndText;
    }
  }
  else if (UIType=="New") {
    //IF speech is currently ON and text is ON, disable text
    if ((bNewSpeech.NormalGraphic==284) && (bNewText.NormalGraphic==289)) {
      bNewText.NormalGraphic = 288;
      Speech.VoiceMode = eSpeechVoiceOnly;
    }
    //IF speech is currently OFF and text is ON, disable text but enable speech
    else if ((bNewSpeech.NormalGraphic==283) && (bNewText.NormalGraphic==289)) {
      bNewSpeech.NormalGraphic = 284;
      bNewText.NormalGraphic = 288;
      Speech.VoiceMode = eSpeechVoiceOnly;
    }
    //IF speech is currently ON and text is OFF, enable both
    else if ((bNewSpeech.NormalGraphic==284) && (bNewText.NormalGraphic==288)) {
      bNewText.NormalGraphic = 289;
      Speech.VoiceMode = eSpeechVoiceAndText;
    }
  }
}

function bSwapUI_OnClick(GUIControl *control, MouseButton button){
  SwapGameUI();
}

function dialog_request(int param) {
  // This is used by the dialog text parser if you need to process
  // text that the player types in to the parser.
  // It is not used by default.
}



function sldSpeed_OnChange(GUIControl *control){
  SetGameSpeed(sldSpeed.Value);
  Watcher.AddMessage( String.Format( "Game speed set: %d", GetGameSpeed() ) );
}

function btnRestart_OnClick(GUIControl *control, MouseButton button){
  ShowRestartGUI();
}

function btnRestartYes_OnClick(GUIControl *control, MouseButton button)
{
  RestartGame();
}

function btnRestartNo_OnClick(GUIControl *control, MouseButton button)
{
  gRestart.Visible = false;
  gIconbar.Visible = true;
  // If the panel's not ON, then the player must have gotten here by tapping F9,
  // therefore his cursor needs restoring. If the panel IS on, then it doesn't,
  // because it's already a pointer. Get used to thinking like this!!
  if ((!gPanel.Visible) || (!gNewPanel.Visible)) mouse.UseDefaultGraphic(); 
}

function btnCancelSave_OnClick(GUIControl *control, MouseButton button)
{
  HideSaveGameGUI();
}
 
function btnSaveGame_OnClick(GUIControl *control, MouseButton button)
{
  int gameSlotToSaveInto = find_save_slot(txtNewSaveName.Text);
  if (gameSlotToSaveInto < 0)
  {
    Display("No more free save slots!");
    return;
  }
  SaveGameSlot(gameSlotToSaveInto, txtNewSaveName.Text);
  HideSaveGameGUI();
}

function btnCancelRestore_OnClick(GUIControl *control, MouseButton button)
{
  HideRestoreGameGUI();
  if (Dead==true) gDeath.Visible = true;
}

function btnRestoreGame_OnClick(GUIControl *control, MouseButton button)
{
  if (lstRestoreGamesList.SelectedIndex >= 0)
  {
    RestoreGameSlot(lstRestoreGamesList.SaveGameSlots[lstRestoreGamesList.SelectedIndex]);
  }
  HideRestoreGameGUI();
}

function lstSaveGamesList_OnSelectionCh(GUIControl *control)
{
  txtNewSaveName.Text = lstSaveGamesList.Items[lstSaveGamesList.SelectedIndex];
}

function txtNewSaveName_OnActivate(GUIControl *control)
{
  // Pressing return in the text box simulates clicking the Save button
  btnSaveGame_OnClick(control, eMouseLeft);
}

function btnDeleteSave_OnClick(GUIControl *control, MouseButton button)
{
  if (lstSaveGamesList.SelectedIndex >= 0)
  {
    DeleteSaveSlot(lstSaveGamesList.SaveGameSlots[lstSaveGamesList.SelectedIndex]);
    lstSaveGamesList.FillSaveGameList();
  }
}

function bSettings_OnClick(GUIControl *control, MouseButton button)
{
  if (UIType=="Classic") if (UIType=="New") gNewPanel.Visible = true; else gPanel.Visible = true;
  else if (UIType=="New") gNewPanel.Visible = true;
  gIconbar.Visible=false;
  mouse.UseModeGraphic(eModePointer);
}



// DEFAULT MESSAGES
// ================

function cEgo_Look()
{
  Display("It's you, Roger Wilco!");
}

function cEgo_Interact()
{
  Display("You could do with a change of clothes. Who knows how many centuries you've been wearing this uniform?");
}

function cEgo_Talk()
{
  cEgo.SpeechView = 2;
  player.Say("I'm Roger Wilco!");
  cEgo.SpeechView = 5;
  player.Say("I don't know why I just said that.");
}

function cEgo_Mode8()
{
  Display("Not gonna lie, you smell pretty bad. That's to be expected, of course, given that you woke up after a prolonged period in suspended animation. But it's still gross.");
}

function cEgo_Mode9()
{
  Display("Who are you kidding. That never goes well.");
}

function unhandled_event(int what, int type) {
  
  // Default look
  if (((what==1) && (type==1)) || ((what==2) && (type==0)) || ((what==4) && (type==1))) {
    int message = Random(2);
    if (message==0) Display("That's so fantastically boring to behold, you have no words for it.");
    if (message==1) Display("That seems to have no remarkable features.");
    if (message==2) Display("You're suddenly overwhelmed with an intense feeling of boredom and decide to look at something else.");
  }
  
  // Default interact
  if (((what==1) && (type==2)) || ((what==2) && (type==1)) || ((what==4) && (type==2))) {
    int message = Random(2);
    if (message==0) Display("That's not for you to mess with.");
    if (message==1) Display("Keep your hands off that.");
    if (message==2) Display("Not everything in the galaxy deserves your handprint on it, Roger.");
  }
  
  // Default talk
  if (((what==1) && (type==4)) || ((what==2) && (type==2)) || ((what==4) && (type==4))) {
    int message = Random(2);
    if (message==0) Display("Lucky for you, it doesn't talk back.");
    if (message==1) Display("It's not exactly what you might call a sparkling conversationalist.");
    if (message==2) Display("It's not much of a talker, Roger.");
  }
  
  // Default smell
  // if (((what==1) && (type==8)) || ((what==2) && (type==6))) {
  if ((mouse.Mode==eModeSmell) && ((IsInteractionAvailable(mouse.x, mouse.y, eModeSmell) == 0))) {
    int message = Random(2);
    if (message==0) Display("Keep your sniffer to yourself.");
    if (message==1) Display("Your nose did not enjoy that one bit.");
    if (message==2) Display("You're not sure what smells worse - that, or your uniform.");
  }
  
  // Default taste
  if ((mouse.Mode==eModeTaste) && ((IsInteractionAvailable(mouse.x, mouse.y, eModeTaste) == 0))) {
    int message = Random(2);
    if (message==0) Display("Eww. Were you seriously going to put your tongue on that?");
    if (message==1) Display("I know it gets lonely in space but get it together, man.");
    if (message==2) Display("That does not deserve a tongue bath, especially not from you.");
  }
  
  // Default inventory
  if (((what==1) && (type==3)) || ((what==2) && (type==3)) || ((what==4) && (type==3))) {
    if (player.ActiveInventory==iBuckazoid) Display("Don't throw your money away!");
    else if (player.ActiveInventory==iMonolithRing) Display("That doesn't look like a coded message.");
    else if (player.ActiveInventory==iMonolithMeal) Display("No, Roger. Food goes in mouth.");
    else if (player.ActiveInventory==iInvisibelt) Display("No way! You went through a lot of trouble to get this belt. Don't just dump it anywhere.");
    else if (player.ActiveInventory==iGem) Display("The gem has lost its glow and wouldn't be of much use here.");
    else if (player.ActiveInventory==iLadder) Display("You don't need to scale that.");
    else {
      int message = Random(2);
      if (message==0) Display("You can't use the %s here.", player.ActiveInventory.Name);
      if (message==1) Display("That's no place for the %s.", player.ActiveInventory.Name);
      if (message==2) Display("If you throw the %s there, you might never get it back!", player.ActiveInventory.Name);
    }
  }
  
  // Look at character
  if ((what==3) && (type==0)) Display("This person doesn't look very interesting.");
  
  // Use character
  if ((what==3) && (type==1)) {
    int message = Random(2);
    if (message==0) Display("Most people would find that a bit presumptuous.");
    if (message==1) Display("That's a bit forward, don't you think?");
    if (message==2) Display("Your understanding of people's personal space leaves a lot to be desired.");
  }
  
  // Talk to character
  if ((what==3) && (type==2)) {
    int message = Random(2);
    if (message==0) Display("They don't appear to want to speak to you.");
    if (message==1) Display("Your attempts to strike up a conversation fail spectacularly.");
    if (message==2) Display("You're not here to make friends, Roger.");
  }
  
  // Inventory on character
  if ((what==3) && (type==3)) {
    Display("Don't give the %s away! You might need it later.");
  }
  
  // Smell character
  if ((what==3) && (type==6)) {
    int message = Random(2);
    if (message==0) Display("You'll have to curtail your canine behavior here, Roger.");
    if (message==1) Display("People don't take kindly to strange janitors walking up and sniffing them.");
    if (message==2) Display("Stop sniffing people, Roger. We've been over this.");
  }
  
  // Taste character
  if ((what==3) && (type==7)) {
    int message = Random(2);
    if (message==0) Display("Use it and lose it, buddy.");
    if (message==1) Display("At least buy them dinner first.");
    if (message==2) Display("You may have been in deep space for an indeterminate amount of time but you're not THAT lonely.");
  }
}




// DEATH
// =====

function YoureDead()
{
  Dead = true;
  gIconbar.Visible = false;
  mouse.UseModeGraphic(eModePointer);
  gDeath.Visible = true;
}

function bDeathRestore_OnClick(GUIControl *control, MouseButton button)
{
  gDeath.Visible = false;
  gRestoreGame.Visible = true;
  lstRestoreGamesList.FillSaveGameList();
  mouse.UseModeGraphic(eModePointer);
}

function bDeathRestart_OnClick(GUIControl *control, MouseButton button)
{
  gDeath.Visible = false;
  RestartGame();
}

function bDeathQuit_OnClick(GUIControl *control, MouseButton button)
{
  gDeath.Visible = false;
  Display("Thank you for playing Space Quest III VGA! We're sorry it had to be this way. Do come back soon.");
  QuitGame(0);
}


// GRABBER ACTIONS
// ===============


function bActivateGrabber_OnClick(GUIControl *control, MouseButton button)
{
  if (player.Room==8) {
    // If Roger is BACKGROUND, check if he's in position to grab the motivator
    if (player.y==135) {
      if (GrabberHasMotivator==false) {
        if ((player.x>160) && (player.x<200) && (MotivatorDumpedYay==false)) {
          GrabberGetMotivator = true;
        }
        else GrabberGetMotivator = false;
      }
     
      GrabberActive=true;
      player.ChangeView(13);
      if (GrabberHasMotivator==true) {
        object[5].Visible = true;
        object[5].X = ((player.x)-9); object[3].Y = ((player.y)-5);
        object[5].Move(((player.x)-9), 210, 1, eBlock, eAnywhere);
      }
      else {
        object[3].Visible = true;
        object[3].X = ((player.x)-9); object[3].Y = ((player.y)-5);
        object[3].Move(((player.x)-9), 210, 1, eBlock, eAnywhere);
      }
      
      if (player.x<640) player.ChangeRoom(1);
      else player.ChangeRoom(3);      
    }
    
    
    // If Roger is FOREGROUND, check if he's in a position to dump the motivator
    else if (player.y==195) {
      if (GrabberHasMotivator==true) {
        if (player.x>880) {
          GrabberDumpMotivator = true;
        }
        else GrabberDumpMotivator = false;
      }
      
      GrabberActive = true;
      player.ChangeView(12);
      if (GrabberHasMotivator==true) {
        object[6].Visible = true;
        object[6].X = ((player.x)-16); object[6].Y = ((player.y)-5);
        object[6].Move(((player.x)-16), 210, 1, eBlock, eAnywhere);
      }
      else {
        object[4].Visible = true;
        object[4].X = ((player.x)-16); object[4].Y = ((player.y)-5);
        object[4].Move(((player.x)-16), 210, 1, eBlock, eAnywhere);
      }
      
      if (player.x>640) player.ChangeRoom(4);
      else player.ChangeRoom(6);
    }
    
  }
  else if (player.Room==7) Display("The claw is programmed not to work in this area.");
}




// INVENTORY ITEMS
// ==================

function iBuckazoid_Look(){
  if (Buckazoids==0) Display("These are buckazoids. As in, money. But right now, you don't actually have any. You're broke!");
  else Display("You possess ", Buckazoids, " of these nifty monetary units known as 'buckazoids'.");
}

function cEgo_UseInv()
{
  Display("You attempt to shove it up your nose but luckily fail.");
}


// CHARACTERS
// ==========

// Garbage Freighter robot as character

function cControllerRobot_Interact()
{
  Display("The robot is too far away.");
}

function cControllerRobot_Look()
{

}

function cControllerRobot_Mode8()
{
  Display("You can't tell from here but he probably smells of efficiency.");
}

function cControllerRobot_Talk()
{
  player.SpeechView = 5;
  player.FaceCharacter(cControllerRobot);
  player.Say("Um, excuse me?");
  Wait(20);
  Display("The robot does not appear to be voice activated.");
}

function cControllerRobot_Mode9()
{
  player.FaceCharacter(cControllerRobot);
  Display("He probably wouldn't appreciate you distracting him with your bizarre behavior.");
}





function sldNewSpeedSlider_OnChange(GUIControl *control){
  SetGameSpeed(sldNewSpeedSlider.Value);
  Watcher.AddMessage( String.Format( "Game speed set: %d", GetGameSpeed() ) );
}



function iGem_Look(){
  Display( "You are still carrying the piece of orium you picked up on Labion during your last adventure. However, it has long since lost its glow." );
}

function iWire_Look(){
  Display( "It's a piece of SQ-approved electrical wire." );
}

function iGenerator_Look(){
  Display( "This is an auxiliary reactor." );
}

function iLadder_Look(){
  Display( "This is a ladder. The evenly spaced rungs allow altitude adjustment." );
}

function iMonolithMeal_Look(){
  Display( "A bag chock-full of gastric delights!" );
}

function iMonolithMeal_Interact(){
  Display( "You have to sit down to eat." );
}

function iMonolithRing_Look(){
  Display( "With this ring, you can decode any secret message! Well, almost any secret message." );
}

function iMonolithRing_Interact(){
  Display( "You decide to leave the ring in the relative safety of your pocket." );
}

function iOratStick_Look(){
  Display( "Orat on a Stick! You can open his mouth, and close his mouth! Hours of fun for all!" );
}

function iThermoUndies_Look(){
  Display( "ThermoWeave Shorts. They keep you oool, and they're oh, so stylish." );
}

function iAstroHat_Look(){
  Display( "Wow! Your Offiical Astro Chicken Flight Hat! Man, the babes'll really dig you in this!" );
}

function iInvisibelt_Look(){
  Display( "Terminator's invisibility belt. Look out, it's low on power." );
}

function iPole_Look(){
  Display( "A handy metal pole." );
}

function iThemeralDetonator_Look(){
  Display( "Used for blowing stuff to little bits. It has an impact switch, so in other words... DON'T DROP IT!" );
}

function iCoverals_Look(){
  Display( "A pair of janitor's coveralls. Used for looking the part." );
}

function iVaporizer_Look(){
  Display( "Mr. Garbage: a janitor's best friend. Designed to vaporize all nonorganic biodegradable matter (i.e. trash).");
}

function iElmoPoster_Look(){
  Display( "A fine likeness of Elmo Pug's mug. Elmo Pug is the dashing young owner of ScumSoft, Inc." );
}

function iElmoPosterCopy_Look(){
Display( "A fine likeness of Elmo Pug's mug." );
}

function iIDcard_Look(){
  Display( "Elmo Pug's personal keycard for opening locked doors." );
}

function btnShowSave_gNewPanel_OnClick(GUIControl *control, MouseButton button){
  ShowSaveGameGUI();
}

function btnShowRestore_gNewPanel_OnClick(GUIControl *control, MouseButton button){
  ShowRestoreGameGUI();
}

function btnShowSave_gPanel_OnClick(GUIControl *control, MouseButton button){
  ShowSaveGameGUI();
}

function btnShowRestore_gPanel_OnClick(GUIControl *control, MouseButton button){
  ShowRestoreGameGUI();
}


function on_event (EventType event, int data){
}

function btnQuit_gQuit_OnClick(GUIControl *control, MouseButton button){
  QuitGame(0);
}

function btnPlay_gQuit_OnClick(GUIControl *control, MouseButton button){
  HideQuitGUI();
}

function btnRestart_gRestart_OnClick(GUIControl *control, MouseButton button){
  RestartGame();
}

function btnPlay_gRestart_OnClick(GUIControl *control, MouseButton button){
  HideRestartGUI();
}
